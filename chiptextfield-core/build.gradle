plugins {
    alias(libs.plugins.android.kotlin.multiplatform)
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.compose.multiplatform)
    alias(libs.plugins.mavenpublish)
    alias(libs.plugins.compose.compiler)
}

kotlin {
    jvmToolchain(11)

    androidLibrary {
        namespace = "com.dokar.chiptextfield.core"
        compileSdk = libs.versions.androidCompileSdk.get().toInteger()
        minSdk = libs.versions.androidMinSdk.get().toInteger()
    }

    jvm()
    iosX64()
    iosArm64()
    iosSimulatorArm64()
//    macosX64()
    wasmJs {
        compilerOptions {
            outputModuleName.set("chiptextfield-core")
        }
        browser {
            commonWebpackConfig {
                outputFileName = "chiptextfield-core.js"
            }
            testTask {
                // Tests are broken now: Module not found: Error: Can't resolve './skiko.mjs'
                enabled = false
            }
        }
        binaries.library()
    }
    js(IR) {
        compilerOptions {
            outputModuleName.set("chiptextfield-core-jscanvas")
        }
        browser {
            commonWebpackConfig {
                outputFileName = "chiptextfield-core-jscanvas.js"
            }
        }
        binaries.library()
    }

    sourceSets {
        commonMain {
            dependencies {
                api(libs.jetbrains.compose.ui)
                api(libs.jetbrains.compose.foundation)
            }
        }
    }
}
