plugins {
    alias(libs.plugins.android.kotlin.multiplatform)
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.compose.multiplatform)
    alias(libs.plugins.mavenpublish)
    alias(libs.plugins.compose.compiler)
}

kotlin {
    jvmToolchain(11)

    androidLibrary {
        namespace = "com.dokar.chiptextfield.m3"
        compileSdk = libs.versions.androidCompileSdk.get().toInteger()
        minSdk = libs.versions.androidMinSdk.get().toInteger()
    }

    jvm()
//    iosX64()
//    macosX64()
    wasmJs {
        compilerOptions {
            outputModuleName.set("chiptextfield-m3")
        }
        browser {
            commonWebpackConfig {
                outputFileName = "chiptextfield-m3.js"
            }
            testTask {
                // Tests are broken now: Module not found: Error: Can't resolve './skiko.mjs'
                enabled = false
            }
        }
        binaries.library()
    }
    js(IR) {
        compilerOptions {
            outputModuleName.set("chiptextfield-m3-jscanvas")
        }
        browser {
            commonWebpackConfig {
                outputFileName = "chiptextfield-m3-jscanvas.js"
            }
        }
        binaries.library()
    }

    sourceSets {
        commonMain {
            dependencies {
                api project(":chiptextfield-core")
                api(libs.jetbrains.compose.ui)
                api(libs.jetbrains.compose.foundation)
                api(libs.jetbrains.compose.material3)
            }
        }
    }
}
