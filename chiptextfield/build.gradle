plugins {
    alias(libs.plugins.android.kotlin.multiplatform)
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.compose.multiplatform)
    alias(libs.plugins.mavenpublish)
    alias(libs.plugins.compose.compiler)
}

kotlin {
    jvmToolchain(11)

    androidLibrary {
        namespace = "com.dokar.chiptextfield"
        compileSdk = libs.versions.androidCompileSdk.get().toInteger()
        minSdk = libs.versions.androidMinSdk.get().toInteger()
    }

    jvm()
//    iosX64()
//    macosX64()
    wasmJs {
        compilerOptions {
            outputModuleName.set("chiptextfield")
        }
        browser {
            commonWebpackConfig {
                outputFileName = "chiptextfield.js"
            }
        }
        binaries.library()
    }
    js(IR) {
        compilerOptions {
            outputModuleName.set("chiptextfield-jscanvas")
        }
        browser {
            commonWebpackConfig {
                outputFileName = "chiptextfield-jscanvas.js"
            }
        }
        binaries.library()
    }

    sourceSets {
        commonMain {
            dependencies {
                api project(":chiptextfield-core")
                api(libs.jetbrains.compose.ui)
                api(libs.jetbrains.compose.foundation)
                api(libs.jetbrains.compose.material)
            }
        }
    }
}
